import org.gradle.internal.jvm.Jvm

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61' apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    group pluginGroup
    version pluginVersion

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava.options.encoding = 'UTF-8'
    javadoc.options.encoding = 'UTF-8'

    repositories {
        mavenCentral() // junit
        maven {
            name = 'spigotmc-repo'
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/groups/public/'
        }
        maven { url = "https://jitpack.io/"} //github library
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }
}

subprojects {
    apply plugin: 'maven-publish'

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'

        Project api = project(':api')
        if (project != api)
            compile api
    }

    Project api = project(':api')

    if (project != api) {
        project.tasks.forEach {
            if (it.getName() != 'clean') {
                it.onlyIf {
                    gradle.taskGraph.hasTask(':shadowJar') || parent.hasProperty('withNMS')
                }
            }
        }
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar) {
        archiveClassifier.set 'javadoc'
        from javadoc
    }

    publishing {
        publications {
            Tap(MavenPublication) {
                artifactId = "$parent.name-$project.name"
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = parent.name + "-" + project.name
                    description = 'Noonmaru minecraft library'
                    url = 'https://github.com/noonmaru/tap'
                    licenses {
                        license {
                            name = 'GPL-2.0'
                            url = 'http://www.opensource.org/licenses/gpl-2.0.php'
                        }
                    }
                    developers {
                        developer {
                            id = 'noonmaru'
                            name = 'Noonmaru'
                            email = 'noonmaru10@gmail.com'
                            url = 'https://github.com/noonmaru'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/noonmaru/tap.git'
                        developerConnection = 'scm:git:ssh://github.com/noonmaru/tap.git'
                        url = 'https://github.com/noonmaru/tap'
                    }
                }
            }
        }
    }
}

project(':api') {
    processResources {
        filesMatching('**/*.yml') {
            expand project.properties
        }
    }

    repositories {
        maven {
            name = 'papermc-repo'
            url = 'https://papermc.io/repo/repository/maven-public/'
        }
    }

    dependencies {
        compileOnly files(Jvm.current().toolsJar)
        implementation 'com.destroystokyo.paper:paper-api:1.15.2-R0.1-SNAPSHOT'
        compile group: 'org.ow2.asm', name: 'asm', version: '7.0'
    }
}

shadowJar {
    archiveClassifier.set 'dist'

    dependencies {
        exclude(dependency('com.google.code.gson:gson'))
    }
}

dependencies {
    compile subprojects
}


task spigotJar(type: Copy) {
    from shadowJar
    into 'W:\\Servers\\test\\plugins'
}

tasks.build.finalizedBy(spigotJar)